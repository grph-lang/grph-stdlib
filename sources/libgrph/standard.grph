//
//  standard.grph
//  GRPH stdlib - libgrph
//
//  Created by Emil Pedersen on 13/02/2022.
//
//  This Source Code Form is subject to the terms of the Mozilla Public
//  License, v. 2.0. If a copy of the MPL was not distributed with this
//  file, You can obtain one at https://mozilla.org/MPL/2.0/.
//

#requires GRPH 1.11
#compiler strictest true

#import stdio

integer? grph_standard_stringToInteger[string string] = string as? integer
float? grph_standard_stringToFloat[string string] = string as? float

float grph_standard_getXForPos[pos p] = p.x
float grph_standard_getYForPos[pos p] = p.y

#function string do_concat[{mixed} text, string sep]
	string total = ""
	boolean first = true
	#foreach elem : text
		#if first
			first = false
		#else
			total += sep
		total += elem as string
	#return total

#function string grph_standard_concat[mixed text...]
	#return do_concat[text ""]

#function string grph_standard_toString[mixed text...]
	#return do_concat[text " "]

#function string grph_standard_log[mixed text...]
	string result = do_concat[text " "]
	printOut: result
	printOut: "\n"
	#return result

#function {integer} grph_standard_range[integer _first, integer _last, integer _step = 1]
	integer first = _first
	integer last = _last
	integer step = _step
	// #if step == 0
	// 	#throw InvalidArgumentException("step cannot be 0")
	#if step < 0
		step = -step
	#if first > last
		step = -step

	{integer} result = ()
	integer i = first

	#while [i <= last && step > 0] || [i >= last && step < 0]
		result{+} = i
		i += step

	#return result
